apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

configureJacoco {
    srcDirs = kotlin.sourceSets.commonMain.kotlin.srcDirs
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    libraryVariants.all { it.generateBuildConfig.enabled = false }
}

kotlin {
    targets {
        fromPreset(presets.android, 'android')

        fromPreset(presets.iosX64, 'ios') {
            binaries { framework([DEBUG]) { baseName = "devtools" } }
            binaries { framework([RELEASE]) { baseName = "devtools" } }
        }

        fromPreset(presets.iosArm64, 'iosArm64') {
            binaries { framework { baseName = "devtools" } }
            sourceSets["iosArm64Main"].dependsOn(sourceSets["iosMain"])
        }

        fromPreset(presets.iosArm32, 'iosArm32') {
            binaries { framework { baseName = "devtools" } }
            sourceSets["iosArm32Main"].dependsOn(sourceSets["iosMain"])
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.20.0"
            }
        }

        androidMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
            }
        }

        iosMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.20.0"
            }
        }

        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
                implementation "io.mockk:mockk:1.9.3"
                implementation "io.mockk:mockk-common:1.9.3"
            }
        }

        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test"
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
    }

    task debugFatFramework(type: org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask) {
        baseName = "devtools"
        destinationDir = file("$buildDir/fat-framework/debug")
        from(
                targets.ios.binaries.getFramework("DEBUG"),
                targets.iosArm32.binaries.getFramework("DEBUG"),
                targets.iosArm64.binaries.getFramework("DEBUG")
        )
    }

    task releaseFatFramework(type: org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask) {
        baseName = "devtools"
        destinationDir = file("$buildDir/fat-framework/release")
        from(
                targets.ios.binaries.getFramework("RELEASE"),
                targets.iosArm32.binaries.getFramework("RELEASE"),
                targets.iosArm64.binaries.getFramework("RELEASE")
        )
    }

    afterEvaluate {
        tasks.forEach { task ->
            def taskName = task.name
            if(taskName.toLowerCase().contains("ios") && taskName.toLowerCase().contains("test")) {
                task.setEnabled(false)
            }
            if (taskName.contains("assembleDebug")) {
                task.dependsOn "debugFatFramework"
            }
            if (taskName.contains("assembleRelease")) {
                task.dependsOn "releaseFatFramework"
            }
        }
    }
}

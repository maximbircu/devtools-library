apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply from: "$rootDir/gradle/publishing.gradle"

configureJacoco {
    srcDirs = android.sourceSets.main.kotlin.srcDirs
    excludes = [
            '**/*Fragment*',
            '**/*Activity*.*',
            '**/*Layout*.*',
            '**/*Adapter*.*',
            '**/*Dialog*.*'
    ]
}

android {
    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    sourceSets {
        main.java.srcDir 'src/main/kotlin'
        test.java.srcDir 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    libraryVariants.all { it.generateBuildConfig.enabled = false }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    api project(':devtools:common')

    implementation "org.yaml:snakeyaml:1.29"
    implementation "com.google.android.material:material:1.5.0"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // androidx
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.fragment:fragment-ktx:1.4.1"

    // test
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation "io.mockk:mockk:1.12.0"
}

afterEvaluate {
    configurePublishing { artifactId = "devtools-android" }
}

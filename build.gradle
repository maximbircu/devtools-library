import org.jetbrains.kotlin.gradle.dsl.KotlinMultiplatformExtension

buildscript {
    ext.kotlin_version = "1.6.10"
    repositories {
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.18.0-RC3"
}

apply from: 'gradle/detekt.gradle'
apply from: 'gradle/lint/lint.gradle'
apply from: 'gradle/ktlint.gradle'
apply from: 'gradle/jacoco.gradle'

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    repositories {
        google()
        mavenCentral()
    }

    afterEvaluate {
        def kmpExt = project.extensions.findByType(TypeOf.typeOf(KotlinMultiplatformExtension))
        kmpExt?.sourceSets?.removeAll { it.name == "androidAndroidTestRelease" }
    }

    configurations.all {
        resolutionStrategy.eachDependency {
            if (requested.group == "org.jetbrains.kotlin") {
                useVersion(kotlin_version)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
